wrapper {
    gradleVersion = '7.6.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

ext.libraries = [
    project(':simple-mapfile-core'),
    project(':simple-mapfile-index'),
    project(':simple-mapfile-xml'),
]

configure(libraries) {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
}

configure(subprojects.findAll {it !in libraries}) {
    apply plugin: 'java'
}

subprojects {
    apply plugin: 'eclipse'

    repositories {
        maven {
            url 'https://mvn.topobyte.de'
        }
        maven {
            url 'https://mvn.slimjars.com'
        }
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

configure(libraries) {
    java {
         withSourcesJar()
    }

    def global = new File('info.pom');
    def local = new File(project.projectDir, 'info.pom');

    def pomInfo = new XmlSlurper().parse(global);
    if (local.exists()) {
        def pomInfoL = new XmlSlurper().parse(local);
        pomInfo.name = pomInfoL.name
    }

    if (project.hasProperty("topobyte")) {
        apply from: 'file:///' + System.getProperty("user.home") \
            + '/.gradle/topobyte.gradle'
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                pom {
                    name = "$pomInfo.name"
                    description = "$pomInfo.description"
                    url = "$pomInfo.url"
                    licenses {
                        license {
                            name = "$pomInfo.licenseName"
                            url = "$pomInfo.licenseUrl"
                            distribution = "$pomInfo.licenseDistribution"
                        }
                    }
                }
            }
        }
    }
}
